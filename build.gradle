apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'idea'
apply plugin: 'net.researchgate.release'

idea {
    project.languageLevel = '1.8'
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath group: 'com.github.ben-manes', name: 'gradle-versions-plugin', version: '0.10.1'
        classpath group: 'net.researchgate', name: 'gradle-release', version: '2.1.0'
    }
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {
        compile group: 'com.google.guava', name: 'guava', version: '18.0'
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'
        compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.12'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
}

task aggregateJavadoc(type: Javadoc) {
    group 'Documentation'
    description 'Generates aggregated Javadoc API documentation for the main source code of all projects.'

    source subprojects.collect { project ->
        project.sourceSets.main.allJava
    }
    classpath = files(subprojects.collect { project ->
        project.sourceSets.main.compileClasspath
    })

    destinationDir file("${rootDir}/docs/javadoc/")

    options.showAll()

    configure(options) {
        windowTitle 'RuneScape API'
        docTitle 'RuneScape API'

        links 'http://docs.oracle.com/javase/8/docs/api/'
        links 'https://commons.apache.org/proper/commons-csv/apidocs/'
        links 'https://hc.apache.org/httpcomponents-client-ga/httpclient/apidocs/'
        links 'http://google-gson.googlecode.com/svn/trunk/gson/docs/javadocs/'
        links 'http://docs.guava-libraries.googlecode.com/git-history/release/javadoc/'
        links 'http://junit.org/javadoc/latest/'
        links 'http://www.slf4j.org/api/'
    }
}

project(':shared') {
    dependencies {
        compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
        compile group: 'org.apache.commons', name: 'commons-csv', version: '1.1'
        compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.4.1'
    }
}

project(':bestiary') {
    dependencies {
        compile project(':shared')
    }
}

project(':ge') {
    dependencies {
        compile project(':shared')
    }
}

project(':hiscores') {
    dependencies {
        compile project(':shared')
    }
}

project(':api') {
    dependencies {
        compile project(':bestiary')
        compile project(':ge')
        compile project(':hiscores')
    }
}
