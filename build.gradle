plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'java'
apply plugin: 'maven-publish'

defaultTasks 'clean', 'build'

repositories {
    mavenCentral()
}

dependencies {
    compile "com.google.code.gson:gson:$gsonVersion"
    compile "com.google.guava:guava:$guavaVersion"
    compile "org.apache.commons:commons-csv:$commonsCsvVersion"
    compile "org.apache.httpcomponents:httpclient:$httpClientVersion"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.slf4j:slf4j-jdk14:$slf4jVersion"
    testCompile "junit:junit:$junitVersion"
    testCompile "org.hamcrest:hamcrest-all:$hamcrestVersion"
}

javadoc {
    title = 'RuneScape API'
    source = sourceSets.main.allJava
    classpath = configurations.compile
    failOnError = true

    options {
        links 'http://docs.oracle.com/javase/8/docs/api/'
        links "https://google.github.io/gson/apidocs/"
        links "http://google.github.io/guava/releases/$guavaVersion/api/docs/"
        links "https://commons.apache.org/proper/commons-csv/archives/$commonsCsvVersion/apidocs/"
        links 'https://hc.apache.org/httpcomponents-client-ga/httpclient/apidocs/'
        links "https://www.slf4j.org/api/"
        links "https://junit.org/junit4/javadoc/$junitVersion/"
    }
}

task javadocJar(type: Jar) {
    group = LifecycleBasePlugin.BUILD_GROUP
    description = 'Assembles a jar archive containing the Javadoc API documentation.'
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    group = LifecycleBasePlugin.BUILD_GROUP
    description = 'Assembles a jar archive containing the main classes with sources.'
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact javadocJar
            artifact sourcesJar
        }
    }
}
